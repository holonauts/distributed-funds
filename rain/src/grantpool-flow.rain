---
/* coupon signer */
#notary-address !notary-address must be the the coupon signer

/* accepted payment token */
#payment-token  !payment-token must be the accepted token address


#deposit
you: context<0 0>(),
me: context<0 1>(),
payment-amount: context<1 0>(),

/* ensure deposits are open */
is-closed: get(hash("is-closed")),
:ensure(not(eq(is-closed 1)) "deposits are closed"),

/* deposit into contract */
_ _ _ _: payment-token you me payment-amount,

/* account that a deposit has been made */
prev-amount: get(hash("total-deposited")),
:set(hash("total-deposited") add(prev-amount payment-amount));


#close
you: context<0 0>(),
me: context<0 1>(),
payment-amount: context<1 0>(),

/* ensure not closed already */
is-closed: get(hash("is-closed")),
:ensure(not(eq(is-closed 1)) "deposits are already closed"),

/* ensure you are the notary */
:ensure(eq(you notary-address) "only the notary can close deposits"),

/* account that a deposit has been made */
:set(hash("is-closed") 1);


#claim
/**
 * for claiming, users will use the signed coupon by the game master,
 * each user calls with the same coupon
 * 
 * it is passed as signed context
 * 
 * the coupon will be a signed message with the following fields:
 * [0] this contract address
 * [1] application evm wallet
 * [2] application ActionHash (core 32 bytes)
 * [3] application amount
 * [4] total deposit pool amount
 */
you: context<0 0>(),
me: context<0 1>(),

coupon-signer: context<2 0>(),

/* coupon contents */
contract: context<3 0>(),
application-address: context<3 1>(),
application-action-hash-core32: context<3 2>(),
application-amount: context<3 3>(),
expected-total-deposited: context<3 4>(),

/* ensure that the coupon is signed by the notary */
:ensure(eq(coupon-signer notary-address) "coupon must be signed by notary"),

/* ensure that the coupon is for this contract */
:ensure(eq(contract me) "coupon must be for this contract"),

/* ensure the application-address is the caller */
:ensure(eq(you application-address) "caller must be application address"),

/* ensure application has not claimed yet */
application-has-claimed: get(application-action-hash-core32),
:ensure(eq(application-has-claimed 0) "application funding has already been claimed"),

/* ensure the coupon know the total amount deposited in the pool  */
total-deposited: get(hash("total-deposited")),
:ensure(eq(expected-total-deposited total-deposited) "coupon total deposited must match actual total deposited"),

/* transfer amount to application-address */
_ _ _ _: payment-token me you application-amount,

/* mark that application has claimed */
:set(application-action-hash-core32 1);